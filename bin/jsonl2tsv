#!/usr/bin/env jqsh
# Usage: (see shpecs)

opts --unbuffered --raw-output

arg_array   cols        ${cols:-$*}
arg         missing_key ${missing_key:-Â¿}
arg_boolean headers     ${headers:-true}


__JQ__


( $cols     | map(. | split(":")[ 0]                  )) as $keys       |
( $cols     | map(. | split(".")[-1] | split(":")[-1] )) as $headings   |

(
  if $headers and (input_line_number < 2) then
    # if the key matches the heading then use the heading
    # else prepend : to the heading
    [ $keys, $headings ] |
      transpose |
      map(
      if .[0] == .[1] then
        .[1]
      else
        ":\(.[1])"
      end
    )
  else
    empty
  end
),
(
  . as $row |
  (
    $keys |
    map(
      ( . | split(".") ) as $subkeys                  |  # split the col name on "." & into subkeys
      reduce $subkeys[] as $subkey (
        $row;
        if type == "object" and has($subkey) then .[$subkey] else $missing_key end
      )                                               |  # dive deeper into the row by reducing with subkeys
      tostring
    )                                                    # pull out each col from each row and convert to string
  )
) |
@tsv                                                     # convert to tsv
